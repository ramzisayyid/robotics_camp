' -----[ Title ]--------------------------------------------------------------
' Robotics with the Boe-Bot - FollowingBoeBot.bs2
' Boe-Bot adjusts its position to keep objects it detects in zone 2.

' Connections:
'    PORT  4          SPEAKER
'    PORTS 2 & 8      IR LED with 1 KOhm in series
'    PORTS 0 & 9      IR Receiver with 220 Ohm resistor

' {$STAMP BS2}                               ' Stamp directive.
' {$PBASIC 2.5}                              ' PBASIC directive.

DEBUG "Program Running!"

' -----[ Constants ]----------------------------------------------------------

Kpl            CON -35
Kpr            CON 35
SetPoint       CON 2
CenterPulse    CON 750

' -----[ Variables ]----------------------------------------------------------

freqSelect     VAR     Nib
irFrequency    VAR     Word
irDetectLeft   VAR     Bit
irDetectRight  VAR     Bit
distanceLeft   VAR     Nib
distanceRight  VAR     Nib
pulseLeft      VAR     Word
pulseRight     VAR     Word

' -----[ Initialization ]-----------------------------------------------------

FREQOUT 4, 2000, 3000

' -----[ Main Routine ]-------------------------------------------------------

DO
  ' Check for objects. distanceLeft and distanceRight are set to a value
  ' between 0 and 5 (5 meaning that an object is close).
  GOSUB Get_Ir_Distances


  ' We would like to have follow an object is zone 2, assuming we find an
  ' object in zone 3 (far), the object we are following is too close, and we
  ' set the stepper motor to move faster. If we find an object in zone 1 (close),
  ' we set the stepper motor to move slower.

  ' If the object we are following veers right or left, our sensors will detect
  ' see that and will adjust the speeds of the motors to compensate.

  ' Calculate proportional output.
  pulseLeft =  SetPoint - distanceLeft  * Kpl + CenterPulse
  pulseRight = SetPoint - distanceRight * Kpr + CenterPulse

  ' After we have calculated the speeds for each motor, we can just send
  ' the pulses to move the stepper motors.
  GOSUB Send_Pulse

LOOP

' -----[ Subroutine - Get IR Distances ]--------------------------------------
Get_Ir_Distances:
  distanceLeft = 0
  distanceRight = 0

  FOR freqSelect = 0 TO 4
    ' Check if we detect an object with each of the frequencies below.
    ' The higher the frequency, the more near sighted the Boe-Bot is.

    ' Check Figure 8.2 on page 257 of the book for a picture that shows
    ' the distance detection frequencies for the Boe-Bot.
    LOOKUP freqSelect,[37500,38250,39500,40500,41500], irFrequency


    ' For each of the values in the lookup, we check if we can detect an
    ' object with each sensor. If we do, we increase distanceLeft or
    ' distanceRight by 1.

    ' If distanceLeft or distanceRight is high, then the object is close,
    ' otherwise, it is far. If it's 0, the sensor couldn't detect an object.
    FREQOUT 8,1,irFrequency
    irDetectLeft = IN9
    distanceLeft = distanceLeft + irDetectLeft

    FREQOUT 2,1,irFrequency
    irDetectRight = IN0
    distanceRight = distanceRight + irDetectRight
  NEXT
  RETURN

' -----[ Subroutine – Get Pulse ]---------------------------------------------

Send_Pulse:
  PULSOUT 13,pulseLeft
  PULSOUT 12,pulseRight
  PAUSE 5
  RETURN