' Robotics with the Boe-Bot - FastIrRoaming.bs2
' Higher performance IR object detection assisted navigation

' Left and Right LEDs will be on when an object is detected
' with the corresponding sensor.

' Connections:
'    PORT  4          SPEAKER
'    PORTS 2 & 8      IR LED with 1 KOhm in series
'    PORTS 0 & 9      IR Receiver with 220 Ohm resistor
'    PORTS 1 & 10     LED with 220 Ohm resistor in series

' {$STAMP BS2}
' {$PBASIC 2.5}

DEBUG "Program Running!"

irDetectLeft VAR Bit
irDetectRight VAR Bit
pulseLeft VAR Word
pulseRight VAR Word

FREQOUT 4, 2000, 3000          ' Signal program start/reset.


DO
  FREQOUT 8, 1, 38500
  irDetectLeft = IN9
  FREQOUT 2, 1, 38500
  irDetectRight = IN0

  IF (irDetectLeft = 0) AND (irDetectRight = 0) THEN
    ' Object straight ahead, back up
    HIGH 10
    HIGH 1
    pulseLeft = 650
    pulseRight = 850
  ELSEIF (irDetectLeft = 0) THEN
    ' Detected something on the left, turn right
    HIGH 10
    LOW 1
    pulseLeft = 850
    pulseRight = 850
  ELSEIF (irDetectRight = 0) THEN
    ' Detected an object on the right, turn left
    LOW 10
    HIGH 1
    pulseLeft = 650
    pulseRight = 650
  ELSE
    ' No object detected, keep going straight
    LOW 10
    LOW 1
    pulseLeft = 850
    pulseRight = 650
  ENDIF

  PULSOUT 13, pulseLeft
  PULSOUT 12, pulseRight

  PAUSE 15
LOOP